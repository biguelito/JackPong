/**
Player class
Move the player up and down
*/
class Player{
	field int x, y, xsize, ysize, score;
	field char mvup, mvdown, mv; 

	/**
	Create the player on the given side
	*/
	constructor Player new(int tox, char tomvup, char tomvdown){
		do Screen.setColor(true);
		let score = 0;
		let xsize = 6;
		let ysize = 40;
		let mvup = tomvup;
		let mvdown = tomvdown;

		let y = 112; 
		let x = tox; 

		do Screen.drawRectangle(x,y,x+xsize,y+ysize);

		return this;
	}

	/**
	Check if the player need to move up or down
	*/
	method void move(char k){
		if ((k = mvup) | (k = mvdown)){
			let mv = k;
		}
		if (mv = mvup){
			do moveup();
		}
		if (mv = mvdown){
			do movedown();
		}
		
		return;
	} 

	/**
	Move the player up 1 pixel on y axis
	*/
	method void moveup(){
		if (y > 25){
			do Screen.setColor(false);
			do Screen.drawRectangle(x, (y + ysize) - 1, x + xsize, y + ysize);
			let y = y - 1;
			do Screen.setColor(true);
			do Screen.drawRectangle(x, y, x + xsize, y+1);
		}
		return;
	}

	/**
	Move the player down 1 pixel on y axis
	*/
	method void movedown(){
		if ((y + ysize) < 255){
			do Screen.setColor(false);
			do Screen.drawRectangle(x, y, x + xsize, y + 1);
			let y = y + 1;
			do Screen.setColor(true);
			do Screen.drawRectangle(x, (y + ysize) - 1, x + xsize, y + ysize);
		}
		return; 
	}

	/**
	Increase the score of this player
	*/
	method void increaseScore(){
		let score = score + 1;
		return;
	}

	/**
	Return the y axis
	*/
	method int getY(){
		return y;
	}

	/**
	Return the size on the y axis
	*/
	method int getYSize(){
		return ysize;
	}
	
	/**
	Return the score of this player
	*/
	method int getScore(){
		return score;
	}

	/**
	Dispose this player
	*/
	method void dispose() {
      do Memory.deAlloc(this);
      return;
   }
}	